%start Parol
%title "Parol grammar"
%comment "Parol's own grammar"
%line_comment "//"
%block_comment "/\*" "\*/"

%%

Parol
    : Prolog GrammarDefinition
    ;

Prolog
    : StartDeclaration { GlobalDirective };

GlobalDirective
    : Declaration
    | ScannerState
    ;

StartDeclaration
    : "%start" Identifier
    ;

Declaration
    : "%title" String
    | "%comment" String
    | ScannerDirectives
    ;

ScannerDirectives
    : "%line_comment" String
    | "%block_comment" String String
    | "%auto_newline_off"
    | "%auto_ws_off"
    ;

GrammarDefinition
    : "%%" Production { Production }
    ;

Production
    : Identifier ":" Alternations ";"
    ;

Alternations
    : Alternation { "\|" Alternation }
    ;

Alternation
    : { Factor }
    ;

Factor
    : Group
    | Repeat
    | Optional
    | Symbol
    ;

Symbol
    : Identifier
    | SimpleToken
    | TokenWithStates
    ;

SimpleToken
    : String
    ;

TokenWithStates
    :  "<" StateList ">" String
    ;

Group
    : "\(" Alternations "\)"
    ;

Optional
    : "\[" Alternations "\]"
    ;

Repeat
    : "\{" Alternations "\}"
    ;

Identifier
    : "[a-zA-Z_]\w*"
    ;

String
    : "\u{0022}([^\\]|\\.)*?\u{0022}"
    ;

ScannerState
    : "%scanner" Identifier "\{" { ScannerDirectives } "\}"
    ;

StateList
    : Identifier StateListRest
    ;

StateListRest
    : "," Identifier StateListRest
    |
    ;